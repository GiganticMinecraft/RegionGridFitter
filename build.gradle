buildscript {
    ext.kotlin_version = '2.1.10'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'click.seichi'
version '0.1.0-alpha'

apply plugin: 'kotlin'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    // For FastAsyncWorldEdit
    maven {
        name = 'sonatype-nexus'
        url = 'https://eldonexus.de/repository/maven-public/'
    }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    embed "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT'

    /**
     * 最新パッチによる API の削除などを回避するため，FastAsyncWorldEdit API はバージョンを v2.9.2 に固定しておく必要がある．
     * Renovate はこの依存関係についての更新は無視するようになっている．
     * 詳細: https://github.com/GiganticMinecraft/RegionGridFitter/issues/26
     */
    implementation 'com.fastasyncworldedit:FastAsyncWorldEdit-Core:2.9.2'
    implementation 'com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit:2.9.2'

    implementation fileTree(dir: "libs", include: "*.jar")
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

compileKotlin {
    kotlinOptions.jvmTarget = "${jvm_version}"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "${jvm_version}"
}
